apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.kbaldauf.playerfinder"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "0.1." + computeBuildNumber()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "STATTLESHIP_API_TOKEN", "\""+getStattleshipApiToken()+"\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def getStattleshipApiToken() {
    if (System.env.CI || System.env.CIRCLECI) { // Use environment variable if built on CI
        return "$System.env.STATTLESHIP_API_TOKEN";
    } else {
        def Properties props = new Properties() // Use properties file if built locally
        props.load(new FileInputStream(new File('keys.properties')))
        return props.getProperty("stattleshipApiToken")
    }
}

def computeBuildNumber() {
    if (System.env.BUILD_NUMBER) {              // Check if there is a system build number.
        return "$System.env.BUILD_NUMBER".toInteger()
    } else if (System.env.CIRCLE_BUILD_NUM) {   // Check to see if this is built on Circle CI
        return "$System.env.CIRCLE_BUILD_NUM".toInteger()
    } else if (System.env.BUDDYBUILD_BUILD_NUMBER) {   // Check to see if this is built on BuddyBuild
        return "$System.env.BUDDYBUILD_BUILD_NUMBER".toInteger()
    } else {
        return 0                                // if none is found, default to 1.
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha5'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    compile 'com.vikingsen:pocketknife-core:3.2.0'
    provided 'com.vikingsen:pocketknife-compiler:3.2.0'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'com.google.dagger:dagger:2.6'
    apt 'com.google.dagger:dagger-compiler:2.6'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'
    debugCompile 'com.squareup.haha:haha:2.0.3' // explicitly added to resolve leakcanary error
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}
